/* About: This File
Show information about the script including the name, development
status, version, last build date, and other info.

Section: Globals

This file is part of the JMP OSI PI Tools Add-In.

This Add-In is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.

This Add-In is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with the Add-In. If not, see <https://www.gnu.org/licenses/>.
*/

Names default to here( 1 );

// About: Customize this file
// Set this to the add-in ID
thisAddIn = "com.github.himanga.JMPOSIPITools";

// About: get or make namespace for this add-in
If( 
	!Namespace Exists( Eval( thisAddIn ) || ".addinnamespace" ),
	//reload the add-in
	ns = New Namespace( Eval( thisAddIn ) || ".addinnamespace" );
	ns:addinhome = "$ADDIN_HOME(" || thisAddIn || ")\";
	Include( ns:addinhome || "addinLoad.jsl" );,
	ns = Namespace( Eval( thisAddIn ) || ".addinnamespace" )
);

ns:logusage( "About" );

// Function: padnum
// Add a zero before single digit numbers, return text.
padnum = function( {x},
	if( x < 10, "0", "" ) || char(x);
);

// Routine: About Window
// Shows info about this function
Eval( Eval Expr( aboutwin = new window( "About",
	v list box(
		text box( ns:metadata_aa["name"], << set width( 500 );, << Set font size( 24 ); ),
		text box( <<set width( 500 ) ),
		text box( "Developed by: " || ns:metadata_aa["developer"] || ".\!N" || 
			ns:metadata_aa["developeremail"] || "\!N", << set width( 500 ) ),
		text box( ns:metadata_aa["abouttext"] || "\!N", << set width( 500 ) ),
		spacer box( size( 10, 10 ) ),
		bb = Button Box( "Open Documentation",
			Web( Expr( "file://" || Convert File Path( ns:addinhome ) || "Docs/index.html#File:Help/Help.txt" ), JMP window ),
			<< Style(Underline)
		),
		bb = Button Box( "View the Readme",
			Web( Expr( "file://" || Convert File Path( ns:addinhome ) || "README.txt" ), JMP window ),
			<< Style(Underline)
		),
		spacer box( size( 10, 10 ) ),
		h list box(
			V List Box(
				Spacer Box( Size( 0, 2 ) ),
				text box( "This add-in is licensed via" )
			),
			bb = Button Box( "GPL v3.0",
				Web( Expr( "file://" || Convert File Path( ns:addinhome ) || "LICENSE.txt" ), JMP window ),
				<< Style(Underline)
			)
		),
		spacer box( size( 10, 10 ) ),
		h list box(
			V List Box(
				Spacer Box( Size( 0, 2 ) ),
				text box( "Current version: " || char( ns:metadata_aa["addinVersion"] ) || " (" || ns:metadata_aa["state"] || ")" )
			),
			bb = Button Box( "Change Log",
				Web( Expr( "file://" || Convert File Path( ns:addinhome ) || "CHANGELOG.txt" ), JMP window ),
				<< Style(Underline)
			),
		),
		text box( "Build Date: " || format(ns:metadata_aa["buildDate"] , "yyyy-mm-dd" ) || " " || 
			padnum(hour( ns:metadata_aa["buildDate"] ))|| ":" || padnum(minute( ns:metadata_aa["buildDate"] )), << set width( 500 ) ),
		text box( "ID: " || char( ns:metadata_aa["id"], << set width( 500 ) ) ),
		spacer box( size( 10, 10 ) ),
		H list box(
			Button Box("Close",
				aboutwin << Close window();
			),
			<< Padding(
				Left( 0 ),
				Top( 20 ),
				Right( 20 ),
				Bottom( 0 )
			)
		),
		<< Padding(
			Left( 20 ),
			Top( 0 ),
			Right( 20 ),
			Bottom( 20 )
		)
	);
) ) ); 